
                                             DSPSIZ(27 132 *DS4) PRINT INDARA

      **************************************************************************
                R IMAPVWAS                   SFL

                  ASLINE       130A  O 04 02

                  ASRECN         6  0H

      **************************************************************************
                R IMAPVWAC                   OVERLAY INVITE
                                             CA03(03) CA04(04)
                                             CA05(05) CA06(06)
                                             CA09(09)
                                             SFLCTL(IMAPVWAS)
                                             SFLPAG(20) SFLSIZ(21)
        20                                   SFLCLR
        21                                   SFLDSPCTL
        22                                   SFLDSP
        23                                   SFLEND(*MORE)
                                             CSRLOC(ACCROW ACCCOL)
                                             RTNCSRLOC(*MOUSE  &ACCROW &ACCCOL)
                                             RTNCSRLOC(&ACCRCD &ACCFLD &ACCPOS)
                                             SFLCSRRRN(&CURCUR)

                  ACHEAD        40A     1  2 MSGID(C000000 *LIBL/IMAPMSG)
                                             COLOR(WHT)
                                        1100 USER
                                        1112 DATE EDTCDE(Y)
                                        1124 TIME
                  CURCUR         5S 0H
                  ACCROW         3S 0H
                  ACCCOL         3S 0H
                  ACCRCD        10A  H
                  ACCFLD        10A  H
                  ACCPOS         4S 0H

                  ACREFR         2S 0B  2130COLOR(TRQ) ALIAS(AC_REFRESH)
                                            RANGE(1 60)
                  ACMAIL        60A  O  2  2ALIAS(AC_MAIL)
                                            COLOR(YLW)
                  ACDEVICE      10A  O  2100
                  ACHEAD0       16A  O  2112MSGID(C000005 *LIBL/IMAPMSG)
                  ACLIN00      130A  O  3  2DSPATR(UL) COLOR(BLU)
                                            MSGID(C000007 *LIBL/IMAPMSG)


      **************************************************************************
                R IMAPVWAF                  OVERLAY
                  AFLIN01      130A  O 25 02DSPATR(UL) COLOR(BLU)
                  AFLIN02      130A  O 26 02MSGID(C000008 *LIBL/IMAPMSG)
                                            COLOR(BLU)

      **************************************************************************
                R IMAPVWW0                  OVERLAY
                                            WINDOW(10 20 09 46 *NOMSGLIN)
                                            WDWBORDER((*CHAR '        ') +
                                                      (*DSPATR RI)       +
                                                      (*COLOR BLU))
                                            CA03(03) CA12(12)
                                            CSRLOC(W0CROW W0CCOL)
                                            RTNCSRLOC(*MOUSE  &W0CROW &W0CCOL)
                                            RTNCSRLOC(&W0CRCD &W0CFLD &W0CPOS)

                  W0CROW         3S 0H
                  W0CCOL         3S 0H
                  W0CRCD        10A  H
                  W0CFLD        10A  H
                  W0CPOS         4S 0H

                  W0CHEAD       30A  O  1  2MSGID(C000009 *LIBL/IMAPMSG)
                  W0CHOST        9A  O  3  2MSGID(C000010 *LIBL/IMAPMSG)
                  W0CUSER        9A  O  4  2MSGID(C000011 *LIBL/IMAPMSG)
                  W0CPASS        9A  O  5  2MSGID(C000012 *LIBL/IMAPMSG)
                  W0CUTLS        9A  O  6  2MSGID(C000013 *LIBL/IMAPMSG)
                  W0CFOOT       30A  O  8  2MSGID(C000014 *LIBL/IMAPMSG)
                                            COLOR(BLU)
                  W0HOST        16A  B  3 12COLOR(TRQ) ALIAS(W0_HOST)
                                            CHECK(LC)
                  W0USER        32A  B  4 12COLOR(TRQ) ALIAS(W0_USER)
                                            CHECK(LC)
                  W0PASS        32A  B  5 12COLOR(TRQ) ALIAS(W0_PASSWORD)
                                            CHECK(LC) DSPATR(ND)
                  W0UTLS         4A  B  6 12COLOR(TRQ) ALIAS(W0_USE_TLS)
                                            VALUES('*YES' '*NO')

